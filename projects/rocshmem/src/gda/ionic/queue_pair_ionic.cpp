/******************************************************************************
 * Copyright (c) Advanced Micro Devices, Inc. All rights reserved.
 *
 * SPDX-License-Identifier: MIT
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 * IN THE SOFTWARE.
 *****************************************************************************/

#include "gda/queue_pair.hpp"
#include "gda/endian.hpp"
#include "util.hpp"
#include "log.hpp"
#include "containers/free_list_impl.hpp"

namespace rocshmem {

__device__ uint32_t QueuePair::reserve_sq(ActiveWFInfo &wf_info,
    uint32_t num_wqes) {
  uint32_t my_sq_prod = 0;

  // reserve space for wqes in sq
  if (wf_info.is_pe_group_first) {
    my_sq_prod = __hip_atomic_fetch_add(&sq_prod, num_wqes, __ATOMIC_RELAXED,
                 __HIP_MEMORY_SCOPE_AGENT);
  }
  my_sq_prod = __shfl(my_sq_prod, wf_info.pe_group_first_phys_lane_id);

  // wait for that space to be available
  ionic_quiet_internal(wf_info, my_sq_prod + num_wqes - sq_mask);

  return my_sq_prod;
}

__device__ uint32_t QueuePair::reserve_sq_single(uint32_t num_wqes) {
  uint32_t my_sq_prod = 0;

  // reserve space for wqes in sq
  my_sq_prod = __hip_atomic_fetch_add(&sq_prod, num_wqes, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);

  // wait for that space to be available
  ionic_quiet_internal_ccqe_single(my_sq_prod + num_wqes - sq_mask);

  return my_sq_prod;
}

__device__ uint32_t QueuePair::commit_sq_single(uint32_t my_sq_prod, [[maybe_unused]] uint32_t my_sq_pos, uint32_t num_wqes) {
  uint32_t dbprod = my_sq_prod + num_wqes;

  spin_lock_acquire_unique(&sq_lock);

  if ((sq_dbprod - dbprod) & (1u << 31)) {
    sq_dbprod = dbprod;

    ionic_ring_doorbell_single(dbprod);
  }

  spin_lock_release_unique(&sq_lock);

  return dbprod;
}

__device__ uint32_t QueuePair::commit_sq(ActiveWFInfo &wf_info,
    uint32_t my_sq_prod, [[maybe_unused]] uint32_t my_sq_pos,
    uint32_t num_wqes) {
  uint32_t dbprod = my_sq_prod + num_wqes;

  spin_lock_acquire_shared(&sq_lock, wf_info.pe_group_mask);

  if (wf_info.is_pe_group_first && ((sq_dbprod - dbprod) & (1u << 31))) {
    sq_dbprod = dbprod;

    ionic_ring_doorbell(dbprod);
  }

  spin_lock_release_shared(&sq_lock, wf_info.pe_group_mask);

  return dbprod;
}

__device__ void QueuePair::poll_wave_cqes(uint64_t activemask) {
  int my_logical_lane_id = get_active_lane_num(activemask);
  uint32_t my_cq_pos = cq_pos + my_logical_lane_id;

  /* Look at the cqe at the current position in the cq buffer */
  struct ionic_v1_cqe *cqe = &ionic_cq_buf[my_cq_pos & cq_mask];

  /* Determine expected color based on cq wrap count */
  uint32_t qtf_color_bit = byteswap<uint32_t>(IONIC_V1_CQE_COLOR);
  uint32_t qtf_color_exp = qtf_color_bit;
  if (my_cq_pos & (cq_mask + 1)) {
    qtf_color_exp = 0;
  }

  /* Check if my cqe color == expected color */
  uint32_t qtf_be = *(volatile uint32_t *)(&cqe->qid_type_flags);
  if ((qtf_be & qtf_color_bit) != qtf_color_exp) {
    return;
  }

  uint32_t msn = byteswap<uint32_t>(cqe->send.msg_msn);

  /* Report if the completion indicates an error. */
  if (!!(qtf_be & byteswap<uint32_t>(IONIC_V1_CQE_ERROR))) {
#if defined(BUILD_DEBUG_DEVICE)
    uint32_t qtf = byteswap<uint32_t>(qtf_be);
    uint32_t qid = qtf >> IONIC_V1_CQE_QID_SHIFT;
    uint32_t type = (qtf >> IONIC_V1_CQE_TYPE_SHIFT) & IONIC_V1_CQE_TYPE_MASK;
    uint32_t flag = qtf & 0xf;
    uint32_t status = byteswap<uint32_t>(cqe->status_length);
    uint64_t npg = cqe->send.npg_wqe_idx_timestamp & IONIC_V1_CQE_WQE_IDX_MASK;

    LOGD_ERROR("QUIET ERROR: qid %u type %u flag %#x status %u msn %u npg %lu",
               qid, type, flag, status, msn, npg);
#endif
    /* No other way to signal an error, so just crash. */
    abort();
  }

  /* Only proceed with the furthest ahead cqe to update the sq state */
  uint64_t my_lane_mask = 1ull << __lane_id();
  uint64_t lesser_lane_mask = my_lane_mask - 1;
  if (my_lane_mask != (__ballot(true) & activemask & ~lesser_lane_mask)) {
    return;
  }

  /* update position in the cq */
  cq_pos = my_cq_pos + 1;

  /*
   * Ring cq doorbell frequently enough to avoid cq full.
   *
   * NB: IONIC_CQ_GRACE is 100
   */
  if (((cq_pos - cq_dbpos) & cq_mask) >= 100) {
    cq_dbpos = cq_pos;
    __atomic_store_n(cq_dbreg, cq_dbval | (cq_mask & cq_dbpos), __ATOMIC_SEQ_CST); //TODO:maybe relaxed?
  }

  sq_msn = msn;
}

__device__ void QueuePair::ionic_quiet_internal_ccqe(ActiveWFInfo &wf_info,
    uint32_t cons) {
  if (!wf_info.is_pe_group_first) {
    return;
  }

  volatile struct ionic_v1_cqe *cqe = &ionic_cq_buf[0];
  uint32_t qtf_be = cqe->qid_type_flags;
  uint32_t msn = byteswap<uint32_t>(cqe->send.msg_msn);
  while ((msn - cons) & 0x800000) {
    if (!!(qtf_be & byteswap<uint32_t>(IONIC_V1_CQE_ERROR))) {
      break;
    }

    qtf_be = cqe->qid_type_flags;
    msn = byteswap<uint32_t>(cqe->send.msg_msn);
  }

  if (!!(qtf_be & byteswap<uint32_t>(IONIC_V1_CQE_ERROR))) {
#if defined(BUILD_DEBUG_DEVICE)
    uint32_t qtf = byteswap<uint32_t>(qtf_be);
    uint32_t qid = qtf >> IONIC_V1_CQE_QID_SHIFT;
    uint32_t type = (qtf >> IONIC_V1_CQE_TYPE_SHIFT) & IONIC_V1_CQE_TYPE_MASK;
    uint32_t flag = qtf & 0xf;
    uint32_t status = byteswap<uint32_t>(cqe->status_length);
    uint64_t npg = cqe->send.npg_wqe_idx_timestamp & IONIC_V1_CQE_WQE_IDX_MASK;

    LOGD_ERROR("QUIET ERROR (CCQE): qid %u type %u flag %#x status %u msn %u npg %lu",
               qid, type, flag, status, msn, npg);
#endif
    /* No other way to signal an error, so just crash. */
    abort();
  }
}

__device__ void QueuePair::ionic_quiet_internal_ccqe_single(uint32_t cons) {
  volatile struct ionic_v1_cqe *cqe = &ionic_cq_buf[0];
  uint32_t qtf_be = cqe->qid_type_flags;
  uint32_t msn = byteswap<uint32_t>(cqe->send.msg_msn);
  while ((msn - cons) & 0x800000) {
    if (!!(qtf_be & byteswap<uint32_t>(IONIC_V1_CQE_ERROR))) {
      break;
    }

    qtf_be = cqe->qid_type_flags;
    msn = byteswap<uint32_t>(cqe->send.msg_msn);
  }

  if (!!(qtf_be & byteswap<uint32_t>(IONIC_V1_CQE_ERROR))) {
#if defined(BUILD_DEBUG_DEVICE)
    uint32_t qtf = byteswap<uint32_t>(qtf_be);
    uint32_t qid = qtf >> IONIC_V1_CQE_QID_SHIFT;
    uint32_t type = (qtf >> IONIC_V1_CQE_TYPE_SHIFT) & IONIC_V1_CQE_TYPE_MASK;
    uint32_t flag = qtf & 0xf;
    uint32_t status = byteswap<uint32_t>(cqe->status_length);
    uint64_t npg = cqe->send.npg_wqe_idx_timestamp & IONIC_V1_CQE_WQE_IDX_MASK;

    LOGD_ERROR("QUIET ERROR (CCQE): qid %u type %u flag %#x status %u msn %u npg %lu",
               qid, type, flag, status, msn, npg);
#endif
    /* No other way to signal an error, so just crash. */
    abort();
  }
}

__device__ void QueuePair::ionic_quiet_internal(ActiveWFInfo &wf_info, uint32_t cons) {
  uint32_t greed = 10;

  if (!cq_mask) {
    ionic_quiet_internal_ccqe(wf_info, cons);
    return;
  }

  /* wait for sq_msn to catch up or pass cons. */
  /* 0x800000 - sign bit for 24-bit fields     */
  while ((sq_msn - cons) & 0x800000) {
    if (!spin_lock_try_acquire_shared(&cq_lock, wf_info.pe_group_mask)) {
      continue;
    }

    /* with lock acquired, this wave polls cqes until caught up */
    while ((sq_msn - cons) & 0x800000) {
      uint32_t old_sq_msn = sq_msn;

      poll_wave_cqes(wf_info.pe_group_mask);

      if (!((sq_msn - cons) & 0x800000)) {
        if (sq_msn == old_sq_msn) {
          break;
        }
        if (!greed) {
          break;
        }
        --greed;
      }
    }

    spin_lock_release_shared(&cq_lock, wf_info.pe_group_mask);
    break;
  }
}

__device__ void QueuePair::ionic_ring_doorbell(uint32_t pos) {
  // When threads write at once to the same address, not all writes reach the bus.
  // Take turns and insert a thread fence between writes to the same address.
  uint64_t activemask = get_active_lane_mask();
  int lane_id         = get_active_lane_num(activemask);
  int lane_count      = get_active_lane_count(activemask);

  for (int i = 0; i < lane_count; i++) {
    if (lane_id == i) {
      __threadfence();
      __atomic_store_n(sq_dbreg, sq_dbval | (sq_mask & pos), __ATOMIC_SEQ_CST);
    }
  }
  __threadfence();
}

__device__ void QueuePair::ionic_ring_doorbell_single(uint32_t pos) {
  // When threads write at once to the same address, not all writes reach the bus.
  // Take turns and insert a thread fence between writes to the same address.
  __threadfence();
  __atomic_store_n(&sq_dbreg[8 * __lane_id()], sq_dbval | (sq_mask & pos), __ATOMIC_SEQ_CST);
}

__device__ void QueuePair::ionic_quiet(ActiveWFInfo &wf_info) {
  ionic_quiet_internal(wf_info, sq_prod);
}

__device__ void QueuePair::ionic_quiet_single() {
  ionic_quiet_internal_ccqe_single(sq_prod);
}

__device__ void QueuePair::ionic_post_wqe_rma(int32_t size, uintptr_t laddr,
    uintptr_t raddr, uint8_t opcode, ActiveWFInfo &wf_info) {
  uint32_t num_wqes = 1;
  if (wf_info.scope == ThreadScope::thread) {
    num_wqes = wf_info.num_pe_group_lanes;
  }

  uint32_t my_sq_prod = reserve_sq(wf_info, num_wqes);

  uint32_t my_sq_pos = my_sq_prod + wf_info.pe_group_logical_lane_id;
  struct ionic_v1_wqe *wqe = &ionic_sq_buf[my_sq_pos & sq_mask];
  uint16_t wqe_flags = 0;

  if (!(my_sq_pos & (sq_mask + 1))) {
    wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_COLOR);
  }

  if (wf_info.is_pe_group_last) {
    wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_SIG);
  }

  // TODO why is this needed?
  if (size && !laddr && opcode == IONIC_V2_OP_RDMA_WRITE) {
    size = 1;
  }

  wqe->base.wqe_idx = my_sq_pos;
  wqe->base.op = opcode;
  wqe->base.num_sge_key = size ? 1 : 0;
  wqe->base.imm_data_key = byteswap<uint32_t>(0);

  wqe->common.rdma.remote_va_high = byteswap<uint32_t>(raddr >> 32);
  wqe->common.rdma.remote_va_low = byteswap<uint32_t>(raddr);
  wqe->common.rdma.remote_rkey = byteswap<uint32_t>(rkey);
  wqe->common.length = byteswap<uint32_t>(size);

  if (size) {
    if (opcode == IONIC_V2_OP_RDMA_WRITE && static_cast<int32_t>(size) <= static_cast<int32_t>(inline_threshold)) {
      wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_INL);
      wqe->base.num_sge_key = 0;
      if (!laddr) {
        // TODO why is this needed?
        wqe->common.pld.data[0] = 1;
      } else {
        memcpy(wqe->common.pld.data, reinterpret_cast<const void*>(laddr), size);
      }
    } else {
      wqe->common.pld.sgl[0].va = byteswap<uint64_t>(laddr);
      wqe->common.pld.sgl[0].len = byteswap<uint32_t>(size);
      wqe->common.pld.sgl[0].lkey = byteswap<uint32_t>(lkey);
    }
  }

  __hip_atomic_store(&wqe->base.flags, wqe_flags, __ATOMIC_RELEASE,
    __HIP_MEMORY_SCOPE_AGENT);

  commit_sq(wf_info, my_sq_prod, my_sq_pos, num_wqes);
}

__device__ void QueuePair::ionic_post_wqe_rma_single(int32_t size,
    uintptr_t laddr, uintptr_t raddr, uint8_t opcode) {
  uint32_t num_wqes = 1;
  uint32_t my_sq_prod = reserve_sq_single(num_wqes);
  uint32_t my_sq_pos = my_sq_prod;
  struct ionic_v1_wqe *wqe = &ionic_sq_buf[my_sq_pos & sq_mask];
  uint16_t wqe_flags = 0;

  if (!(my_sq_pos & (sq_mask + 1))) {
    wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_COLOR);
  }

  wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_SIG);

  // TODO why is this needed?
  if (size && !laddr && opcode == IONIC_V2_OP_RDMA_WRITE) {
    size = 1;
  }

  wqe->base.wqe_idx = my_sq_pos;
  wqe->base.op = opcode;
  wqe->base.num_sge_key = size ? 1 : 0;
  wqe->base.imm_data_key = byteswap<uint32_t>(0);

  wqe->common.rdma.remote_va_high = byteswap<uint32_t>(raddr >> 32);
  wqe->common.rdma.remote_va_low = byteswap<uint32_t>(raddr);
  wqe->common.rdma.remote_rkey = byteswap<uint32_t>(rkey);
  wqe->common.length = byteswap<uint32_t>(size);

  if (size) {
    if (opcode == IONIC_V2_OP_RDMA_WRITE && static_cast<int32_t>(size) <= static_cast<int32_t>(inline_threshold)) {
      wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_INL);
      wqe->base.num_sge_key = 0;
      if (!laddr) {
        // TODO why is this needed?
        wqe->common.pld.data[0] = 1;
      } else {
        memcpy(wqe->common.pld.data, reinterpret_cast<const void*>(laddr), size);
      }
    } else {
      wqe->common.pld.sgl[0].va = byteswap<uint64_t>(laddr);
      wqe->common.pld.sgl[0].len = byteswap<uint32_t>(size);
      wqe->common.pld.sgl[0].lkey = byteswap<uint32_t>(lkey);
    }
  }

  __hip_atomic_store(&wqe->base.flags, wqe_flags, __ATOMIC_RELEASE, __HIP_MEMORY_SCOPE_AGENT);

  commit_sq_single(my_sq_prod, my_sq_pos, num_wqes);
}

__device__ uint64_t QueuePair::ionic_post_wqe_amo([[maybe_unused]] int32_t size, uintptr_t raddr,
    uint8_t opcode, int64_t atomic_data, int64_t atomic_cmp,
    bool fetching, ActiveWFInfo &wf_info) {
  uint32_t num_wqes = wf_info.num_pe_group_lanes;
  uint32_t my_sq_prod = reserve_sq(wf_info, num_wqes);
  uint32_t my_sq_pos = my_sq_prod + wf_info.pe_group_logical_lane_id;
  struct ionic_v1_wqe *wqe = &ionic_sq_buf[my_sq_pos & sq_mask];
  uint16_t wqe_flags = 0;
  uint32_t cons;

  uint64_t* wave_fetch_atomic{nullptr};
  if (fetching) {
    if (wf_info.is_pe_group_first) {
      auto res = fetching_atomic_freelist->pop_front();
      while (!res.success) {
        res = fetching_atomic_freelist->pop_front();
      }
      wave_fetch_atomic = res.value;
    }
    wave_fetch_atomic = (uint64_t*)__shfl((uint64_t)wave_fetch_atomic,
                         wf_info.pe_group_first_phys_lane_id);
  }

  if (!(my_sq_pos & (sq_mask + 1))) {
    wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_COLOR);
  }

  if (wf_info.is_pe_group_last) {
    wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_SIG);
  }

  wqe->base.wqe_idx = my_sq_pos;
  wqe->base.op = opcode;
  wqe->base.num_sge_key = 1;
  wqe->base.imm_data_key = byteswap<uint32_t>(0);

  wqe->atomic_v2.remote_va_high = byteswap<uint32_t>(raddr >> 32);
  wqe->atomic_v2.remote_va_low = byteswap<uint32_t>(raddr);
  wqe->atomic_v2.remote_rkey = byteswap<uint32_t>(rkey);
  wqe->atomic_v2.swap_add_high = byteswap<uint32_t>(atomic_data >> 32);
  wqe->atomic_v2.swap_add_low = byteswap<uint32_t>(atomic_data);
  wqe->atomic_v2.compare_high = byteswap<uint32_t>(atomic_cmp >> 32);
  wqe->atomic_v2.compare_low = byteswap<uint32_t>(atomic_cmp);

  if (fetching) {
    wqe->atomic_v2.local_va =
        byteswap<uint64_t>(reinterpret_cast<uint64_t>(
          wave_fetch_atomic + wf_info.pe_group_logical_lane_id));
    wqe->atomic_v2.lkey = byteswap<uint32_t>(fetching_atomic_lkey);
  } else {
    wqe->atomic_v2.local_va =
        byteswap<uint64_t>(reinterpret_cast<uint64_t>(nonfetching_atomic));
    wqe->atomic_v2.lkey = byteswap<uint32_t>(nonfetching_atomic_lkey);
  }

  __hip_atomic_store(&wqe->base.flags, wqe_flags, __ATOMIC_RELEASE,
    __HIP_MEMORY_SCOPE_AGENT);

  cons = commit_sq(wf_info, my_sq_prod, my_sq_pos, num_wqes);

  uint64_t ret{0};
  if (fetching) {
    ionic_quiet_internal(wf_info, cons);
    ret = wave_fetch_atomic[wf_info.pe_group_logical_lane_id];
    __atomic_signal_fence(__ATOMIC_SEQ_CST);
    if (wf_info.is_pe_group_first) {
      fetching_atomic_freelist->push_back(wave_fetch_atomic);
    }
  }
  return ret;
}

__device__ uint64_t QueuePair::ionic_post_wqe_amo_single([[maybe_unused]] int32_t size,
    uintptr_t raddr, uint8_t opcode, int64_t atomic_data, int64_t atomic_cmp,
    bool fetching) {
  uint32_t num_wqes = 1;
  uint32_t my_sq_prod = reserve_sq_single(num_wqes);
  uint32_t my_sq_pos = my_sq_prod;
  struct ionic_v1_wqe *wqe = &ionic_sq_buf[my_sq_pos & sq_mask];
  uint16_t wqe_flags = 0;
  uint32_t cons;

  uint64_t* wave_fetch_atomic{nullptr};
  if (fetching) {
    auto res = fetching_atomic_freelist->pop_front();
    while (!res.success) {
      res = fetching_atomic_freelist->pop_front();
    }
    wave_fetch_atomic = res.value;
  }

  if (!(my_sq_pos & (sq_mask + 1))) {
    wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_COLOR);
  }

  wqe_flags |= byteswap<uint16_t>(IONIC_V1_FLAG_SIG);

  wqe->base.wqe_idx = my_sq_pos;
  wqe->base.op = opcode;
  wqe->base.num_sge_key = 1;
  wqe->base.imm_data_key = byteswap<uint32_t>(0);

  wqe->atomic_v2.remote_va_high = byteswap<uint32_t>(raddr >> 32);
  wqe->atomic_v2.remote_va_low = byteswap<uint32_t>(raddr);
  wqe->atomic_v2.remote_rkey = byteswap<uint32_t>(rkey);
  wqe->atomic_v2.swap_add_high = byteswap<uint32_t>(atomic_data >> 32);
  wqe->atomic_v2.swap_add_low = byteswap<uint32_t>(atomic_data);
  wqe->atomic_v2.compare_high = byteswap<uint32_t>(atomic_cmp >> 32);
  wqe->atomic_v2.compare_low = byteswap<uint32_t>(atomic_cmp);

  if (fetching) {
    wqe->atomic_v2.local_va = byteswap<uint64_t>(reinterpret_cast<uint64_t>(wave_fetch_atomic));
    wqe->atomic_v2.lkey = byteswap<uint32_t>(fetching_atomic_lkey);
  } else {
    wqe->atomic_v2.local_va = byteswap<uint64_t>(reinterpret_cast<uint64_t>(nonfetching_atomic));
    wqe->atomic_v2.lkey = byteswap<uint32_t>(nonfetching_atomic_lkey);
  }

  __hip_atomic_store(&wqe->base.flags, wqe_flags, __ATOMIC_RELEASE, __HIP_MEMORY_SCOPE_AGENT);

  cons = commit_sq_single(my_sq_prod, my_sq_pos, num_wqes);

  uint64_t ret{0};
  if (fetching) {
    ionic_quiet_internal_ccqe_single(cons);
    ret = wave_fetch_atomic[0];
    __atomic_signal_fence(__ATOMIC_SEQ_CST);
    fetching_atomic_freelist->push_back(wave_fetch_atomic);
  }
  return ret;
}

}  // namespace rocshmem
